plugins {
    id 'java'
    id 'jacoco'
    id 'maven'
    id 'application'
    id 'maven-publish'
    id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '2.2'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id "com.github.johnrengelman.shadow" version "4.0.2"
}

mainClassName = "robotbuilder.RobotBuilder"


if (!hasProperty('releaseType')) {
    WPILibVersion {
        releaseType = 'dev'
        version = '2019.1.1-beta-4'
    }
}

def pubVersion = ''
if (project.hasProperty("publishVersion")) {
    pubVersion = project.publishVersion
} else {
    pubVersion = WPILibVersion.version
}

jacoco {
    toolVersion = "0.8.2"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

lombok {
    version = "1.18.2"
    sha256 = ""
}

if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io" // used to grab JGraph from its Github releases because it's not in maven central
    }
}

dependencies {
    compile 'org.apache.velocity:velocity:1.7'
    compile 'org.yaml:snakeyaml:1.10'
    compile 'com.github.jgraph:jgraphx:v3.3.1.1'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile 'com.sun.activation:javax.activation:1.2.0'
    testCompile 'junit:junit:4.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.2'
    compileOnly 'org.projectlombok:lombok:1.18.2'
}

test {
    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

jar {

    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes('Implementation-Title': 'RobotBuilder',
                    'Implementation-Version': WPILibVersion.version,
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': new Date(),
                    'Built-JDK': System.getProperty('java.version'))
    }
}



publishing {
    publications {
        maven(MavenPublication) {
            artifact(shadowJar) {
                classifier null
            }
            groupId 'edu.wpi.first.wpilib'
            artifactId 'RobotBuilder'
            version pubVersion
        }
    }
}

wrapper {
    gradleVersion = '5.0-milestone-1'
}
